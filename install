#!/usr/bin/env zsh

local base=${ZULU_DIR:-"${ZDOTDIR:-$HOME}/.config/zulu"}

###
# Create the zulu directory structure
###
function _zulu_create_base() {
  local dirs=('bin' 'config' 'init' 'packages' 'share')
  for dir in "${dirs[@]}"; do
    mkdir -p "$base/$dir"
    touch "$base/$dir/.gitkeep"
  done
}

###
# Install the zulu core functions and index
###
function _zulu_install_core() {
  git clone "https://github.com/zulu-zsh/zulu" "$base/.core"
  git clone "https://github.com/zulu-zsh/index" "$base/.index"
}

###
# Use current $PATH to build a pathfile
###
function _zulu_create_path() {
  local pathfile="$base/config/path"
  echo "$base/bin" > $pathfile
  for p in "${(@s/:/)PATH}"; do
    echo "$p" >> $pathfile
  done
}

###
# Use current $fpath to build an fpathfile
###
function _zulu_create_fpath() {
  local fpathfile="$base/config/fpath"
  echo "$base/share" > $fpathfile
  for fp in "${fpath[@]}"; do
    echo $fp >> $fpathfile
  done
}

###
# Use current aliases to build alias file
###
function _zulu_create_aliases() {
  local aliasfile="$base/config/alias"
  echo "" > $aliasfile
  IFS=$'\n'; for a in `alias`; do
    echo "alias $a\n" >> $aliasfile
  done
}

###
# Install the zulu completion definitions
###
function _zulu_install_completion() {
  ln -s "$base/.core/zulu.zsh-completion" "$base/share/_zulu"
}

###
# Install the initialization script
###
function _zulu_install_init() {
  # Add the init script to .zshrc
  local init='
# Initialise zulu plugin manager
source "${ZULU_DIR:-"${ZDOTDIR:-$HOME}/.config/zulu"}/.core/zulu"
zulu init
'

  echo "$init" >> "${ZDOTDIR:-$HOME}/.zshrc"
}

###
# Install the zulu framework
###
function zulu_install() {
  _zulu_create_base
  _zulu_install_core
  _zulu_create_path
  _zulu_create_fpath
  _zulu_create_aliases
  _zulu_install_completion
  _zulu_install_init

  # Source zulu and install dependencies
  source "$base/.core/zulu"
  zulu init
}

zulu_install
